#! /usr/bin/env janet

(use where-defined)

(defn main [& args]
  
  (when (or (= 1 (length args))
            (find |(or (= $ "-h") (= $ "--help")) args))
    (eprint "usage: janet-md-doc MOD [PATH-STRIP-PREFIX]")
    (eprint "Print janet documentation for a given module, links have PATH-STRIP-PREFIX removed.")
    (os/exit 1))

  (def module (args 1))
  (def trim-prefix (or (get args 2) (os/cwd)))

  (print "# " module)
  (print "")

  (each [k meta] (sort-by first (pairs (require module)))
    (when (symbol? k)
      (print "## " module "/" k)
      (when-let [value (get meta :value)]
        (print "type: " (type value) "\n")
        (when-let [[file line] (where-defined value)]
          (unless (string/has-prefix? trim-prefix file)
            (error (string "file path " file " does not have given prefix " trim-prefix)))
          (def formatted-src (string (string/slice file (inc (length trim-prefix))) (if line (string "#L" line) "")))
          (print "[" formatted-src "](" formatted-src ")\n"))))
      (when-let [doc (get meta :doc)]
        (print "```")
        (print (doc-format doc 100))
        (print "```")
        (print "")))

  (print "")
  (print "Docs generated by: https://github.com/andrewchambers/janet-md-doc"))
